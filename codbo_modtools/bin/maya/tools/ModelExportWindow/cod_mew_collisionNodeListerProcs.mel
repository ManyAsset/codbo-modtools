{
   // Select objects in the current scene according to the Node Lister's selection
   global proc COD_MEW_CollisionSelectSceneNode() {
      // Get the selected nodes in the node list
      string $selItems[] = `textScrollList -q -si MEWcollisionNodes_lst`;

      if (size($selItems) > 0) {
         select -cl;

         for ($s in $selItems) {
            if (`objExists $s` == 1) {
               // If the object exists, then select it
               select -add $s;
            }
         }
      }
   }

   // Add the selected scene nodes to the Node Lister list and the IWGlobalNode attribute
   global proc COD_MEW_CollisionAddNodesToLister() {
      int $selIndex[];

      // Get the selected index in the exports list
      $selIndex = `textScrollList -q -sii MEWcollisionExports_lst`;

      // Maker sure only one export is selected
      if (size($selIndex) == 1) {
         // Get the current export nodes
         string $nodeStr = `getAttr ("IWGlobalNode.CollmapExportNodes"+$selIndex[0])`;
         // Get the user's selection
         string $sel[] = `ls -sl`;
         string $newNodes;

         // Add each selected node to the string of names
         for ($s in $sel) {
            $nodeStr += shortNameOf($s);
            $nodeStr += " ";
         }

         // Remove duplicate node names in case user selected something already in the export list
         $newNodes = stringArrayToString( stringArrayRemoveDuplicates( stringToStringArray($nodeStr, " ") ), " ");
         // Add a space to the end so we are ready to add more nodes
         $newNodes += " ";
         // Set the node list attribute on the IWGlobalNode
         setAttr -type "string" ("IWGlobalNode.CollmapExportNodes" + $selIndex[0]) $newNodes;
   
         // Refresh the Node Lister scroll list
         COD_MEW_CollisionUpdateNodeLister $selIndex[0];
      }
   }

   // Remove the Lister's selected nodes from the IWGlobalNode attribute
   global proc COD_MEW_CollisionRemoveNodesFromLister() {
      int $selIndex[];
      select -cl;

      // Get the selected export index in the exports list
      $selIndex = `textScrollList -q -sii MEWcollisionExports_lst`;

      // Get the nodes which are selected in the node lister
      string $selItems[] = `textScrollList -q -si MEWcollisionNodes_lst`;
      // Get the current list of nodes from the IWGlobalNode attribute
      string $nodeStr = `getAttr ("IWGlobalNode.CollmapExportNodes"+$selIndex[0])`;
      string $newNodes;

      // Remove the items in the scroll list from the export nodes attribute string
      $newNodes = stringArrayToString( stringArrayRemoveExact($selItems, stringToStringArray($nodeStr, " ")), " ");
      if (size($newNodes) > 0) $newNodes += " ";
      // Reset the export nodes attribute
      setAttr -type "string" ("IWGlobalNode.CollmapExportNodes" + $selIndex[0]) $newNodes;

      // Refresh the Node Lister scroll list
      COD_MEW_CollisionUpdateNodeLister $selIndex[0];
   }
}